apply plugin: 'com.android.library'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "0.3"

        buildConfigField "String", "AUTHORITY", "\"${getAuthority()}\""
        manifestPlaceholders = [
                trayAuthority: getAuthority()
        ]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile 'com.android.support:support-annotations:20.0.0'
}

String getAuthority() {
    if (project.properties.containsKey("trayAuthority")) {
        def authority = project.properties.get("trayAuthority")
        if (authority.equals("com.example.preferences")) {
            throw new Exception("com.example.preferences isn't a unique authority!!!\n" +
                    "This will prevent your application from getting installed if another app " +
                    "uses the same authority for Tray or any other ContentProvider.")
        }
        return authority
    } else {
        /*throw new Exception("define ext variable trayAuthority in your project build.gradle\n" +
                "Example:\n" +
                "ext.trayAuthority = \"com.example.preferences\"");*/
        return "";
    }
}